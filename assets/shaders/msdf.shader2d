#uniforms
uniform vec2 textureSize;
uniform float distanceFactor;
uniform float fontWeight;
uniform float shadowClipped;
uniform vec4 shadowColor;
uniform vec2 shadowOffset;
uniform float shadowSmoothing;
uniform vec4 innerShadowColor;
uniform float innerShadowRange;

#fragment
float median(float r, float g, float b) {
    return max(min(r, g), min(max(r, g), b));
}

vec4 blend(vec4 src, vec4 dst, float alpha) {
    // src OVER dst porter-duff blending
    float a = src.a + dst.a * (1.0 - src.a);
    vec3 rgb = (src.a * src.rgb + dst.a * dst.rgb * (1.0 - src.a)) / (a == 0.0 ? 1.0 : a);
    return vec4(rgb, a * alpha);
}

float linearstep(float a, float b, float x) {
    return clamp((x - a) / (b - a), 0.0, 1.0);
}

vec4 fragment(Fragment f) {
    // Glyph
    vec4 msdf = sampleTexture(f.texcoord);
    float distance = distanceFactor * (median(msdf.r, msdf.g, msdf.b) + fontWeight - 0.5);
    float glyphAlpha = clamp(distance + 0.5, 0.0, 1.0);
    vec4 glyph = vec4(f.color.rgb, f.color.a * glyphAlpha);

    // Shadow
    distance = sampleTexture(f.texcoord - shadowOffset / textureSize).a + fontWeight;
    float shadowAlpha = linearstep(0.5 - shadowSmoothing, 0.5 + shadowSmoothing, distance) * shadowColor.a;
    shadowAlpha *= 1.0 - glyphAlpha * shadowClipped;
    vec4 shadow = vec4(shadowColor.rgb, shadowAlpha);

    // Inner shadow
    distance = msdf.a + fontWeight;
    float innerShadowAlpha = linearstep(0.5 + innerShadowRange, 0.5, distance) * innerShadowColor.a * glyphAlpha;
    vec4 innerShadow = vec4(innerShadowColor.rgb, innerShadowAlpha);

    return blend(blend(innerShadow, glyph, 1.0), shadow, f.color.a);
}
